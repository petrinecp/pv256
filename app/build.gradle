def VERSION_MAJOR = 0
def VERSION_MINOR = 1
def VERSION_PATCH = 0
def VERSION_BUILD = 0

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    "$project.name-$variant.name-$variant.versionName-${variant.versionCode}.apk"
            )
        }
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    defaultConfig {
        applicationId "cz.muni.fi.pv256.movio.uco396110"
        minSdkVersion 15
        targetSdkVersion 23

        resConfigs "en", "cs", "sk"
        resConfigs "hdpi", "xhdpi", "xxhdpi", "xxxhdpi"

        versionCode VERSION_MAJOR * 10000 + VERSION_MINOR * 1000 + VERSION_PATCH * 100 + VERSION_BUILD
        versionName "${VERSION_MAJOR}.${VERSION_MINOR}.${VERSION_PATCH}.${VERSION_BUILD}"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
        }
    }

    productFlavors {
        primary {
            resValue "string", "app_name", "Movies"
            buildConfigField "boolean" , "LOGGING_ENABLED" , "false"
        }

        pro {
            applicationId "cz.muni.fi.pv256.movio.uco396110.devel"
            resValue "string", "app_name", "Movies PRO"
            buildConfigField "boolean" , "LOGGING_ENABLED" , "true"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.tonicartos:stickygridheaders:1.0.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.squareup.okhttp3:okhttp:3.0.1'
    compile 'com.google.code.gson:gson:2.5'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit2:retrofit:2.0.0-beta3'
    compile 'com.squareup.retrofit2:converter-gson:2.0.0-beta3'
    compile 'net.danlew:android.joda:2.9.2'
    compile 'com.facebook.stetho:stetho:1.3.0'
}
